plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.akira'
version = '1.3.2'

def serverFolder = 'D:/Server/PaperDebug'
def libsFolder = serverFolder + '/libraries'
def versionsFolder = serverFolder + '/versions'
def pluginsFolder = serverFolder + '/plugins'
def pluginsDepFolder = serverFolder + '/plugins_dep'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    compileOnly fileTree(dir: libsFolder, include: ['**/*.jar'])
    compileOnly fileTree(dir: versionsFolder, include: ['**/*.jar'])
}

shadowJar {
    archiveClassifier.set('shadow')

    dependencies {
        exclude(dependency('org.jetbrains:annotations'))
    }

    relocate 'kotlin', 'com.akira.shadow.kotlin'
}

tasks.register('deployPlugin', Copy) {
    dependsOn shadowJar

    from tasks.shadowJar.getArchiveFile()
    into pluginsFolder
    rename { project.name + '.jar' }
}

tasks.register('deployAsDependency', Copy) {
    dependsOn jar

    from tasks.jar.getArchiveFile()
    into pluginsDepFolder
    rename { project.name + '.jar' }
}

tasks.build {
    dependsOn tasks.named('deployPlugin')
    dependsOn tasks.named('deployAsDependency')
}

processResources {
    inputs.property("checkVersion", project.version)

    filesMatching('plugin.yml') { expand(version: project.version) }
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(17)
}